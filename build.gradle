plugins {
	id 'org.springframework.boot' version '2.3.0.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
	id "com.moowork.node" version "1.3.1"
}

group = 'fr.tdeverdiere'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

project(':client') {
	apply plugin: 'com.moowork.node'

	task npmCachedInstall(type: NpmTask) {
		inputs.file("${project.projectDir}/package.json")
		outputs.dir("${project.projectDir}/node_modules")
		outputs.cacheIf { true }
		args = ['install', '--production']
	}

	task buildNpm(type: NpmTask, dependsOn: npmCachedInstall) {
		inputs.file("${project.projectDir}/package.json")
		inputs.dir("src").withPathSensitivity(PathSensitivity.RELATIVE)
		outputs.dir("$buildDir/ui")
		outputs.cacheIf { true }

		args = ['run', 'build', "${project.projectDir}/client"]
	}

	processResources {
		dependsOn buildNpm
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	implementation 'org.webjars:webjars-locator-core'
	implementation 'org.webjars:sockjs-client:1.0.2'
	implementation 'org.webjars:stomp-websocket:2.3.3'
	runtimeOnly 'org.hsqldb:hsqldb:2.5.0'

	runtimeOnly 'mysql:mysql-connector-java'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.security:spring-security-test'


	developmentOnly("org.springframework.boot:spring-boot-devtools")
}

test {
	useJUnitPlatform()
}

node {
	version = "8.11.1"
	download = false
}

